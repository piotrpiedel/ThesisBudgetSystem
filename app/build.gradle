apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    dexOptions {
        maxProcessCount 4
        preDexLibraries false
        javaMaxHeapSize "8g"
    }

    defaultConfig {

        applicationId "piedel.piotr.thesis"
        minSdkVersion 24
        targetSdkVersion 28
        testInstrumentationRunner "${applicationId}.runner.RxAndroidJUnitRunner"
        versionCode 1000
        // Major -> Millions, Minor -> Thousands, Bugfix -> Hundreds. E.g 1.3.72 == 1,003,072
        versionName '0.1.0'

//        buildConfigField("String", "POKEAPI_API_URL",
//                "\"${PokeapiApiUrl}\"")

        multiDexEnabled true

    }

    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        test {
            java.srcDir commonTestDir
        }
        androidTest {
            java.srcDir commonTestDir
        }
    }

    lintOptions {
        textOutput "stdout"
        textReport true
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        abortOnError false
        lintConfig file("$projectDir/lint.xml")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
        exclude 'META-INF/license/LICENSE.base64.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/MANIFEST.MF'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            returnDefaultValues = true
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.squareup.okio:okio:2.1.0'
    }
}

apply from: 'dependencies.gradle'

dependencies {

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.7@aar') {
        transitive = true;
    }

    implementation supportLibs
//    implementation networkLibs
    implementation rxJavaLibs
    implementation googlePlayLibs
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation roomLibs
    implementation 'com.karumi:dexter:5.0.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    implementation('com.github.bumptech.glide:okhttp3-integration:1.4.0') {
        exclude group: 'glide-parent'
    }

    //HTML parse Library
    implementation 'org.jsoup:jsoup:1.11.3'

    //JSON parse Library
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.8.0")
    implementation 'com.squareup.moshi:moshi-adapters:1.8.0'

    //Expandable RecyclerView
    implementation 'com.thoughtbot:expandablerecyclerview:1.3'

    //RxPermission for easy runtime permission management
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.1@aar'
    implementation "com.jakewharton:butterknife:$versions.butterKnife"
    implementation "com.jakewharton.timber:timber:$versions.timber"
    implementation "com.facebook.stetho:stetho-okhttp3:$versions.stethOkhttp"

    implementation ("com.github.bumptech.glide:recyclerview-integration:4.8.0") {
        // Excludes the support library because it's already included by Glide.
        transitive = false
    }

    // RoundedLetterView library  for Put First Letter on View in RoundImageView
    implementation 'com.github.pavlospt:roundedletterview:1.2'


//    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'

    debugImplementation 'com.readystatesoftware.chuck:library:1.0.4'
    releaseImplementation 'com.readystatesoftware.chuck:library-no-op:1.0.4'

    debugImplementation('com.github.ajitsing:sherlock:1.0.2@aar') {
        transitive = true
    }
    releaseImplementation('com.github.ajitsing:sherlock-no-op:1.0.2@aar')

    implementation 'com.tspoon.traceur:traceur:1.0.1'

    implementation "com.google.dagger:dagger:$versions.dagger"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28' //Required by Dagger2

    debugImplementation leakCannary
    releaseImplementation leakCannary
    testImplementation leakCannary

    kapt "com.jakewharton:butterknife-compiler:$versions.butterKnife"
    kapt daggerCompiler
    kapt roomCompiler
    kaptTest daggerCompiler
    kaptAndroidTest daggerCompiler

    testImplementation unitTestLibs
    androidTestImplementation androidTestsLibs
}

// Log out test results to console
tasks.matching { it instanceof Test }.all {
    testLogging.events = ["failed", "passed", "skipped"]
}
